# Maintainer: Thomas Wei√üschuh <thomas_weissschuh lavabit com>
# Contributor: fauno <fauno@kiwwwi.com.ar>
# Contributor: SanskritFritz


pkgname=transmission-remote-cli
pkgver=1.4.6
pkgrel=1
pkgdesc="Curses interface for the daemon of the BitTorrent client Transmission"
arch=('any')
url="https://github.com/fagga/transmission-remote-cli"
license=('GPL3')
depends=('python2')
optdepends=('python2-geoip: Guess which country peers come from'
            'adns-python: Resolve IPs to host names') 
source=("$pkgname-$pkgver.tar.gz::https://github.com/fagga/transmission-remote-cli/tarball/v${pkgver}")


build() {
  cd "$srcdir/$(ls -1td fagga-transmission-remote-cli-* | head -n 1)"

  sed -i "1s/python/python2/" transmission-remote-cli

}

package() {
  cd "$srcdir/$(ls -1td fagga-transmission-remote-cli-* | head -n 1)"

  install -D -m755 "transmission-remote-cli" "${pkgdir}/usr/bin/transmission-remote-cli"
  install -D -m644 "transmission-remote-cli.1" "${pkgdir}/usr/share/man/man1/transmission-remote-cli.1"
  install -D -m755 "transmission-remote-cli-bash-completion.sh" "${pkgdir}/usr/share/bash-completion/completions/transmission-remote-cli"
}
sha256sums=('ae7644b2d5a4699653e7435cf88b6a994081da4e96570de472a0f00a768290dc')
