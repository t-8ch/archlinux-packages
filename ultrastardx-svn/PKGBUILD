# Contributor: zoulnix <http://goo.gl/HQaP>
pkgname=ultrastardx-svn
pkgver=2787
pkgrel=1
pkgdesc="UltraStar Deluxe (USDX) is a free and open source karaoke game."
arch=('i686' 'x86_64')
url="http://ultrastardeluxe.org/"
license=('GPL')
depends=('ffmpeg' 'libpng12' 'lua' 'portaudio' 'sdl_image' 'sqlite3' 'ttf-dejavu' 'ttf-freefont' 'wqy-microhei')
makedepends=('fpc' 'gcc' 'make' 'pkg-config' 'subversion')
options=('!libtool')
provides=('ultrastardx')
conflicts=('ultrastardx')
groups=('usdx')
source=()
md5sums=('')

_svnmod="ultrastardx"
_svntrunk="https://ultrastardx.svn.sourceforge.net/svnroot/ultrastardx/trunk"

build() { 
  cd ${srcdir}

  #####
  msg "Getting sources..."
  if [ -d ${_svnmod}/.svn ]; then
    cd ${_svnmod} && svn up -r ${pkgver}
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
    cd ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  #####

  export libpng_VERSION=12
  export libavcodec_VERSION=52.86.0
  export libavformat_VERSION=52.78.0
  export libavutil_VERSION=50.24.0
  export libswscale_VERSION=0.11.0

  ./configure --prefix=/usr \
	      --sysconfdir=/etc \
	      --localstatedir=/var \
	      --disable-static

  make LDFLAGS="" || return 1
}

package() {
  cd ${srcdir}/${_svnmod}
  install -d ${pkgdir}/usr/share/{applications,pixmaps} || return 1

  make LDFLAGS="" DESTDIR=${pkgdir} install || return 1

  install -m644 dists/${_svnmod}.desktop \
		${pkgdir}/usr/share/applications/ || return 1
  install -m644 ${pkgdir}/usr/share/${_svnmod}/resources/icons/${_svnmod}-icon.png \
		${pkgdir}/usr/share/pixmaps/${_svnmod}.png || return 1

  # symlink fonts...
  rm ${pkgdir}/usr/share/${_svnmod}/fonts/*/*.tt*
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf ${pkgdir}/usr/share/${_svnmod}/fonts/DejaVu/ || return 1
  ln -s /usr/share/fonts/TTF/DejaVuSans-Bold.ttf ${pkgdir}/usr/share/${_svnmod}/fonts/DejaVu/ || return 1
  ln -s /usr/share/fonts/TTF/FreeSans.ttf ${pkgdir}/usr/share/${_svnmod}/fonts/FreeSans/ || return 1
  ln -s /usr/share/fonts/TTF/FreeSansBold.ttf ${pkgdir}/usr/share/${_svnmod}/fonts/FreeSans/ || return 1
  ln -s /usr/share/fonts/wenquanyi/wqy-microhei/wqy-microhei.ttc ${pkgdir}/usr/share/${_svnmod}/fonts/wqy-microhei/ || return 1

  # Removing unnecessary stuff
  find ${pkgdir} -type f -name "AUTHORS*" -exec rm {} \;
  find ${pkgdir} -type f -name "COPYING*" -exec rm {} \;
  find ${pkgdir} -type f -name "CREDITS" -exec rm {} \;
  find ${pkgdir} -type f -name "LICENSE*" -exec rm {} \;
  find ${pkgdir} -type f -name "README*" -exec rm {} \;
}
