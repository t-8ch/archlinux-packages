# Maintainer: Thomas Wei√üschuh <thomas_weissschuh lavabit.com>

pkgname=palm-novacom-git
pkgver=20120413
pkgrel=1
pkgdesc="WebOS <-> PC connection, open source version"
arch=('x86_64' 'i686')
url="http://openwebosproject.org/"
license=('Apache')
depends=('libusb-compat')
makedepends=('git')
provides=('palm-novacom')
conflicts=('palm-novacom')
source=('novacomd-rc.d' 'novacomd-conf.d' 'novacomd.service'
        '0001-Use-usb_bulk_-read-write-instead-of-homemade-handler.patch'
        '0002-Fix-makefile-so-it-respects-environmental-CFLAGS.patch'
        '0003-Remove-unused-err-variable.patch')

_gitroot=https://github.com/openwebos/novacom
_gitname=novacom

_gitroot2=https://github.com/openwebos/novacomd
_gitname2=novacomd

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi
  if [[ -d "$_gitname2" ]]; then
    cd "$_gitname2" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot2" "$_gitname2"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  rm -rf "$srcdir/$_gitname2-build"

  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  make

  git clone "$srcdir/$_gitname2" "$srcdir/$_gitname2-build"
  cd "$srcdir/$_gitname2-build"

  patch -Np1 -i ${srcdir}/0001-Use-usb_bulk_-read-write-instead-of-homemade-handler.patch
  patch -Np1 -i ${srcdir}/0002-Fix-makefile-so-it-respects-environmental-CFLAGS.patch
  patch -Np1 -i ${srcdir}/0003-Remove-unused-err-variable.patch

  make host
}

package() {
  cd $srcdir
  install -D -m755 novacomd-conf.d $pkgdir/etc/conf.d/novacomd
  install -D -m755 novacomd-rc.d $pkgdir/etc/rc.d/novacomd
  install -D -m755 novacomd.service $pkgdir/usr/lib/systemd/system/novacomd.service

  cd "$srcdir/$_gitname-build"
  install -D -m755 build-novacom/novacom $pkgdir/usr/bin/novacom
  install -D -m755 scripts/novaterm $pkgdir/usr/bin/novaterm


  cd "$srcdir/$_gitname2-build"
  install -D -m755 build-novacomd-host/novacomd $pkgdir/usr/bin/novacomd
}

# vim:set ts=2 sw=2 et:
sha1sums=('1ca416286786a3f0078e66e26ae1c9ef14168b18'
          '239935c21f4e8f2b6d84795ce7c69a5c654ffa98'
          '77adbfefbe6251032d3b753e772793673b04d288'
          '6d00222a1983fd173f4a303ddf3ab5dc424456a8'
          '267af99cbc380bcb6174d450ef3fd933a69553fc'
          '88907b9b639e761d624de4dffa99b92ffddb9de0')
