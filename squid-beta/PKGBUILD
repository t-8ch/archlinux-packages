# Maintainer: Thomas Wei√üschuh <thomas_weissschuh lavabit com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=squid-beta
pkgver=3.2.0.18
pkgrel=1
pkgdesc="A full-featured Web proxy cache server."
arch=('i686' 'x86_64')
url="http://www.squid-cache.org"
depends=('openssl' 'pam' 'cron' 'perl' 'libtool')
makedepends=('libcap')
license=('GPL')
provides=('squid')
conflicts=('squid')
backup=('etc/squid/squid.conf' 'etc/squid/mime.conf' 'etc/conf.d/squid')
install=squid.install
source=("http://www.squid-cache.org/Versions/v3/3.2/squid-${pkgver}.tar.bz2"
        'squid' 'squid.conf.d' 'squid.pam' 'squid.cron' 'squid.service')

build() {
  cd "${srcdir}"/squid-${pkgver}

  # fix cache_dir, cache_dir size, and effective group.
  sed '/^DEFAULT_SWAP_DIR/ s@/cache@/cache/squid@' -i src/Makefile.in
  sed '/^#cache_dir/ s/100/256/
       /^NAME: cache_effective_group/ {n;n;s/none/proxy/}' -i src/cf.data.pre

  ./configure --prefix=/usr --datadir=/usr/share/squid \
      --sysconfdir=/etc/squid --libexecdir=/usr/lib/squid \
      --localstatedir=/var --with-logdir=/var/log/squid \
      --enable-auth --enable-removal-policies="lru,heap" \
      --enable-digest-auth-helpers="password" --enable-storeio="aufs,ufs,diskd" \
      --enable-basic-auth-helpers="getpwnam,YP,NCSA,SMB,MSNT,PAM,multi-domain-NTLM" \
      --enable-external-acl-helpers="ip_user,unix_group,wbinfo_group" \
      --enable-ntlm-auth-helpers="smb_lm,fakeauth,no_check" \
      --enable-delay-pools --enable-arp-acl --enable-ssl --enable-snmp \
      --enable-linux-netfilter --enable-ident-lookups \
      --enable-useragent-log --enable-cache-digests --enable-referer-log \
      --enable-arp-acl --enable-htcp --enable-carp --enable-epoll \
      --with-filedescriptors=4096 --with-large-files --enable-arp-acl \
      --with-default-user=proxy \
      --enable-async-io --enable-truncate
#some versions have build problems with both async and truncate enabled:
#      --enable-async-io

  make
}

package() {
  cd "${srcdir}"/squid-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -D -m755 "${srcdir}"/squid "${pkgdir}"/etc/rc.d/squid
  install -D -m755 "${srcdir}"/squid.cron "${pkgdir}"/etc/cron.weekly/squid
  install -D -m644 "${srcdir}"/squid.conf.d "${pkgdir}"/etc/conf.d/squid
  install -D -m644 "${srcdir}"/squid.pam "${pkgdir}"/etc/pam.d/squid
  install -D -m644 "${srcdir}"/squid.service "${pkgdir}"/ust/lib/systemd/system/squid.service
}
# vim: ts=2 sw=2 et ft=sh
sha512sums=('4507681089874e887f70612e577b7f09d3e6e35028063334ad722e274e672a4b1b8ec4d0d69778a705a9f50a31e3d88cd37cb30bf7cddb8fae9024d109f7c139'
            '889cab6a910912f0dcfaa7757b25dd811293b0628dfaa2783a909f0886a723f0620bfa09e90cf9d06173c21620062da32a7fd9c6b261b0abf243558cfe111bd6'
            '8408b17bc093cd249db06e9113873d7e08791ae8f9ce241b862227c0b2a248c2c3e03bb325d7af4c73120cc5290d8fca802169be36adaa169e767b6b22423e35'
            'c17bb4d6dc1de9b9209a568879d4a1fe762b2274b57190400251bafcfc15a9d9e31b02631c823775cd5cef561d2e5a54cecf4a50647c1eea879ae7a8545a95fb'
            '2e603be3f14a44bd1454c36b342f6926715b9265d78a0e5181f608d463ff2d0347db3a1ee7a1d34c7077e2e21d06f4985f723fceba4a4ee6534d409d49444049'
            '61b5b04296e0da4f04be84d66be3575ae9202f6ff9056021c8098e16cd462186d80ea977a0c0a113653007c1839d3f2690ac7c37079605c12278631871ec9abd')
