# Maintainer: Thomas Wei√üschuh <thomas_weissschuh || lavabit.com>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Sean Middleditch <elanthis@awesomeplay.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=docbook-xsl-ns
pkgver=1.77.1
pkgrel=1
pkgdesc='XML stylesheets for Docbook-xml transformations'
arch=('any')
license=('custom')
url='http://docbook.sourceforge.net/'
depends=('libxml2' 'libxslt' 'docbook-xml')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/docbook/${pkgname}-${pkgver}.tar.bz2")

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	_pkgroot=${pkgdir}/usr/share/xml/docbook/${pkgname}-${pkgver}

        install -dm755 ${_pkgroot}
	install -m644 VERSION VERSION.xsl ${_pkgroot}

	for fn in assembly common eclipse epub epub3 fo highlighting html htmlhelp javahelp lib \
                  manpages params profiling roundtrip template website xhtml xhtml-1_1 xhtml5; do
		install -dm755 ${_pkgroot}/${fn}
		install -m644 ${fn}/*.{xml,xsl,dtd,ent} ${_pkgroot}/${fn} || true  # ignore missing files
	done

	install -dm755 ${pkgdir}/etc/xml
	install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
sha512sums=('f2046a105d6c39714cf96eedb67c9819c61712bdbd1f13c9875fc7ff3d421dd9cd5804d1f60272eee6466d0885faba9135fe71349811ff605f5ea5d2e732c848')
